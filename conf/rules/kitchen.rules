import org.openhab.core.library.types.*

var PercentType civilDuskLevel = new PercentType(25)
var PercentType nauticalDuskLevel = new PercentType(50)
var PercentType astroDuskLevel = new PercentType (100)
var PercentType on = new PercentType(100)
var PercentType off = new PercentType(0)

rule "Civl Dusk Triggered"
when 
    Channel 'astro:sun:local:civilDusk#event' triggered START
then
    logInfo("Kitchen", "Civil dusk rule triggered!")
    logInfo("Kitchen", "Setting lights to {}%", civilDuskLevel)
    sendCommand(LifxKitchenDimmer, civilDuskLevel)   
end

rule "Nautical Dusk Triggered"
when 
    Channel 'astro:sun:local:nauticalDusk#event' triggered START
then
    logInfo("Kitchen", "Nautical dusk rule triggered!")
    logInfo("Kitchen", "Setting lights to {}%", nauticalDuskLevel)
    sendCommand(LifxKitchenDimmer, nauticalDuskLevel)   
end

rule "Astro Dusk Triggered"
when 
    Channel 'astro:sun:local:astroDusk#event' triggered START
then
    logInfo("Kitchen", "Astro dusk rule triggered!")
    logInfo("Kitchen", "Setting lights to {}%", astroDuskLevel)

    sendCommand(LifxKitchenDimmer, astroDuskLevel)      
end

rule "Dimming lights at night"
when
    Time cron "0 30-55 23 1/1 * ? *"
then
    logInfo("Kitchen", "Dimming lights late at night triggered!")

    if (AutoKitchenLightSwitch.state == OFF) {
        logInfo("Kitchen", "Skip dimming the lights, KitchenDimmingSwitch is off")
        return false
    }
   
    var int currentLevel =  (LifxKitchenDimmer.state as DecimalType).intValue

    if (currentLevel > 0) {
        var newLevel = if (currentLevel > 4) new PercentType(currentLevel - 4) else off
        logInfo("Kitchen", "Setting Kitchen Lights to {}%", newLevel) 
        sendCommand(LifxKitchenDimmer, newLevel)
    } else {
        logInfo("Kitchen", "Skip dimming Kitchen Lights since they are already off")
    }
end

rule "Kitchen Switch changed to on"
when
    Item LifxKitchenSwitch changed
then
    if (LifxKitchenSwitch.state == ON) {
        logInfo("Kitchen", "Setting Kitchens Lights to {}%", on)
        sendCommand(LifxKitchenDimmer, on)
    } else if (LifxKitchenSwitch.state == OFF) {
        logInfo("Kitchen", "Setting Kitchens Lights to {}%", off)
        sendCommand(LifxKitchenDimmer, off)
    }
end
